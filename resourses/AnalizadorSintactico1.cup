package codigo3d.backend.analizadores;

import java_cup.runtime.*;
import codigo3d.backend.manejadores.*;
import codigo3d.backend.objetos.*;
import java.util.LinkedList;

parser code {:

	private ManejadorParser mp = null;

	// Connect this parser to a scanner!
	public parser(Lexer1 lex, ManejadorParser mp) {
		super(lex);
		this.mp = mp;
	}

        public void syntax_error(Symbol s) {
            //mp.printSintaxError("Â¡Error de sintaxis! -> "+ s.value +" Linea "+(s.left)+" columna "+(s.right) + "\n");
        }
:}

/* Terminals (tokens returned by the scanner). */
terminal String         BOOLEAN, CHAR, BYTE, INT, LONG, FLOAT, DOUBLE, STRING, NOT;
terminal String 		AND, OR, TRUE, FALSE, ARRAY, IF, ELSIF, ELSE, WHILE;
terminal String 		DO, FOR, PRINT, PRINTLN, COMMA, COLON, OPEN_BRK, CLOSE_BRK;
terminal String 		OPEN_SQR, CLOSE_SQR, SEMICOLON, ASSIGN, PLUS, MINUS, MULTI;
terminal String 		DIVISION, MODULE, LOWER_EQ, HIGHER_EQ, LOWER, HIGHER;
terminal String 		NOT_EQUAL, EQUAL, TEXT, NUM_INTEGER, NUM_DOUBLE, NUM_FLOAT, VARIABLE;
terminal String 		SCANS, SCANN, MAIN, RETURN, SEPARATOR, VOID;

non terminal Cuarteto 	instruction, decl, instructions, block;
non terminal Cuarteto 	programInstruction, programInstructionA, programInstructionB, mainInstruction, globalInstruction;
non terminal Cuarteto 	bodyInstruction, bodyInstructionA, bodySubprogram, instructionB;
non terminal Cuarteto 	assign, assign2, assign1, assign3, formalAssign, decl1, decls, declns, declns1;
non terminal Cuarteto 	arrayAssign, arrayPosition, arrayDim, arrayDim1;
non terminal Cuarteto 	array, assignA; 
non terminal LinkedList <Cuarteto> arrayDimen1, arrayDimen;
non terminal Cuarteto 	if, if1, if2, ifA, elseifA, elseifAA, elseifAA1, elseA;
non terminal Cuarteto 	while, whileA;
non terminal String 	signo;
non terminal Cuarteto 	for, forA;
non terminal Cuarteto 	doWhile, doWhileA;
non terminal Cuarteto 	print, println, printA, printB, value, valueA, valueB;
non terminal String 	rel;
non terminal Tipo 	 	tipo;
non terminal Simbolo 	num, bool;
non terminal Cuarteto 	exp;
non terminal Cuarteto 	opNum, opNum2, ex, ex2;
non terminal Cuarteto 	opBol, b;
non terminal Cuarteto 	opRel, r, expRel, er;
non terminal Cuarteto 	scans, scann;
non terminal Cuarteto 	subprogram, parametros, parametrosA, parametro, rtrn;
non terminal Cuarteto 	subprogramCall, paramsProgram, paramsA;

precedence left 		ELSE;
precedence left 		ELSIF;
precedence left 		OR; 
precedence left 		AND;
precedence left 		NOT;
precedence left 		PLUS;
precedence left 		MINUS;
precedence left 		MULTI, DIVISION, MODULE;
precedence left 		EQUAL, NOT_EQUAL;
precedence left 		HIGHER_EQ, LOWER_EQ;
precedence left 		LOWER, HIGHER;
precedence left 		OPEN_BRK;

start with globalInstruction;

/* INICIO GRAMATICA DE -----3DIRECCIONES----- */

globalInstruction ::= 	bodyInstruction:e1 SEPARATOR mainInstruction:e2 {:mp.print(e1, e2);:}
						| SEPARATOR mainInstruction:e {:mp.print(null, e);:}
						| mainInstruction:e {:mp.print(null, e);:}
						;

mainInstruction ::= 	MAIN:e1 OPEN_BRK CLOSE_BRK {:mp.setSubprogramaActual(e1);mp.setGlobal(false);:} block:e2 {:mp.setGlobal(true);RESULT = e2;:}
						| MAIN:e OPEN_BRK CLOSE_BRK {::}
						| MAIN:e
						;

block ::= 				OPEN_SQR instructions:e CLOSE_SQR {:RESULT = e;:}
						| OPEN_SQR CLOSE_SQR {::}
						;

bodyInstruction ::= 	bodyInstruction:e1 bodyInstructionA:e2 {:RESULT = mp.getInstructions(e1, e2);:}
						| bodyInstructionA:e {:RESULT = e;:}
						;

bodyInstructionA ::=	subprogram:e {:RESULT = e;:}
						| instructionB:e {:RESULT = e;:}
						;

subprogram ::= 			tipo:e1 
						VARIABLE:e2 {:mp.setSubprogramaActual(e2); mp.setGlobal(false);mp.setResultSubprogramActual(mp.getSubprogramResult());:}
						parametrosA:e3
						bodySubprogram:e4 {:mp.verificateSubprogram();RESULT = mp.getSubprograma(e2, e4);mp.setGlobal(true);:}
						;

programInstruction ::= 	programInstruction:e1 programInstructionA:e2 {:RESULT = mp.getInstructions(e1, e2);:}
						| programInstructionA:e {:RESULT = e;:}
						;

programInstructionA ::= programInstructionB:e {:RESULT = e;mp.addSubprogramInstruction(e);:}
						;

programInstructionB ::=	formalAssign:e {:RESULT = e;:}
						| arrayAssign:e {:RESULT = e;:}
						| if:e {:RESULT = e;:}
						| while:e {:RESULT = e;:}
						| for:e {:RESULT = e;:}
						| doWhile:e {:RESULT = e;:}
						| print:e {:RESULT = e;:}
						| println:e {:RESULT = e;:}
						| scans:e {:RESULT = e;:}
						| scann:e {:RESULT = e;:}
						| rtrn:e {:RESULT = e; mp.setSubprogramReturn(true);:}
						;

subprogramCall ::= 		VARIABLE:e1 paramsA:e2 {:RESULT = mp.getSubprogramCall(e1, e2, e1left, e1right);:}
						;

paramsA ::= 			OPEN_BRK CLOSE_BRK {::}
						| OPEN_BRK paramsProgram:e1 CLOSE_BRK {:RESULT = e1;:}
						;

paramsProgram ::= 		paramsProgram:e1 COMMA opNum:e2 {:RESULT = mp.getComponentes(e1, e2);:}
						| opNum:e1 {:RESULT = mp.getComponente(e1);:}
						;

rtrn ::=				RETURN opNum:e SEMICOLON {:RESULT = mp.getReturn(e, eleft, eright);:}
						;

parametrosA ::= 		OPEN_BRK CLOSE_BRK
						| OPEN_BRK parametros CLOSE_BRK
						;

parametros ::= 			parametros:e1 COMMA parametro:e2 
						| parametro:e 
						;

parametro ::= 			tipo:e1 VARIABLE:e2 
						;

bodySubprogram ::= 		OPEN_SQR declns:e1 SEPARATOR programInstruction:e2 CLOSE_SQR {:RESULT = mp.getInstructions(e1, e2);:}
						| OPEN_SQR declns:e CLOSE_SQR {:RESULT = e;:}
						| OPEN_SQR programInstruction:e CLOSE_SQR {:RESULT = e;:}
						| OPEN_SQR CLOSE_SQR {::}
						;

declns ::= 				declns:e1 declns1:e2 
						| declns1:e 
						;

declns1 ::=				decl:e {:RESULT = e;:}
						| array:e {::}
						;

instructions ::= 		instructions:e1 instruction:e2 {:RESULT = mp.getInstructions(e1,e2);:}
						| instruction:e {:RESULT = e;:}
						;

instructionB ::= 		decl:e {:RESULT = e;:}
						| formalAssign:e {:RESULT = e;:}
						| arrayAssign:e {:RESULT = e;:}
						| array:e {::}
						;

instruction ::=  		decl:e {:RESULT = e;:}
						| formalAssign:e {:RESULT = e;:}
						| arrayAssign:e {:RESULT = e;:}
						| array:e {::}
						| if:e {:RESULT = e;:}
						| while:e {:RESULT = e;:}
						| for:e {:RESULT = e;:}
						| doWhile:e {:RESULT = e;:}
						| print:e {:RESULT = e;:}
						| println:e {:RESULT = e;:}
						| scans:e {:RESULT = e;:}
						| scann:e {:RESULT = e;:}
						| rtrn:e {:RESULT = e; mp.setSubprogramReturn(true);:}
						;

decl ::= 				tipo:e1 decls:e2 SEMICOLON {:RESULT = e2;:}
						;

decls ::= 				decls:e1 COMMA decl1:e2 {:RESULT = mp.getInstructions(e1, e2);:}
						| decl1:e {:RESULT = e;:}
						;

decl1 ::= 				assign2:e {:RESULT = e;:}
						| VARIABLE:e {::}
						;

assign2 ::= 			assign3:e1 exp:e2 {:RESULT = mp.asignByToken(e1,e2, e1left, e1right);:}
						;

assign3 ::= 			VARIABLE:e ASSIGN {:RESULT = mp.getVariable(e, eleft, eright);:}
						;

arrayDim ::= 			arrayDim:e1 arrayDim1:e2 {:RESULT = mp.getInstructions(e1, e2);:}
						| arrayDim1:e {:RESULT = e;:}
						;

arrayDim1 ::= 			OPEN_SQR opNum:e CLOSE_SQR {:RESULT = mp.getPosition(e, eleft, eright);:}
						;

arrayDimen ::= 			arrayDimen:e1 arrayDimen1:e2 {::}
						| arrayDimen1:e {::}
						;

arrayDimen1 ::= 		OPEN_SQR opNum2:e CLOSE_SQR {::}
						;

array ::= 				ARRAY tipo:e1 arrayDimen:e2 assignA:e3 SEMICOLON {::}
						;

assignA ::= 			assignA:e1 COMMA VARIABLE:e2 {::}
						| VARIABLE:e {::}
						;

arrayAssign ::= 		arrayPosition:e1 ASSIGN exp:e2 SEMICOLON {:RESULT = mp.setArrayAsign(e1,e2);:}
						;

arrayPosition ::= 		VARIABLE:e1 arrayDim:e2 {:RESULT = mp.getArrayPosition(e1, e2, e1left, e1right);:}
						;

if ::= 					if1:e1 ifA:e2 {:RESULT = mp.ifOperacion(e1, e2);:}
						| if1:e {:RESULT = e;:}
						;

if1 ::= 				IF:e1 {:mp.setEtiquetaResult(mp.getEtiqueta());:} OPEN_BRK opBol:e2 CLOSE_BRK if2:e3 {:RESULT = mp.ifOperacion(e2, e3, e2left, e2right);:}
						;

if2 ::= 				instruction:e {:RESULT = e;:}
						| block:e {:RESULT = e;:}
						;

ifA ::= 				elseifA:e1 elseA:e2 {:RESULT = mp.elseOperacion(e1, e2);:}
						| elseifA:e {:RESULT = e;:}
						| elseA:e {:RESULT = e;:}
						;

elseifA ::= 			elseifA:e1 elseifAA:e2 {:RESULT = mp.elseOperacion(e1, e2);:}
						| elseifAA:e {:RESULT = e;:}
						;

elseifAA ::= 			ELSIF:e1 OPEN_BRK opBol:e2 CLOSE_BRK elseifAA1:e3 {:RESULT = mp.elseIfOperacion(e2, e3, e2left, e2right);System.out.println("ELSE");:}
						;

elseifAA1 ::= 			instruction:e {:RESULT = e;:}
						| block:e {:RESULT = e;:}
						;

elseA ::= 				ELSE:e1 elseifAA1:e2 {:RESULT = mp.elseOperacion(e2, e2left, e2right);:}
						;

while ::= 				WHILE:e1 {:mp.setEtiquetaResult(mp.getEtiqueta());:} OPEN_BRK opBol:e2 CLOSE_BRK whileA:e3 {:RESULT = mp.whileOperacion(e1,e2,e3,e2left,e2right);:}
						;

whileA ::= 				instruction:e {:RESULT = e;:}
						| block:e {:RESULT = e;:}
						;

doWhile ::= 			DO:e1 {:mp.setEtiquetaResult(mp.getEtiqueta());:} doWhileA:e3 WHILE OPEN_BRK opBol:e2 CLOSE_BRK {:RESULT = mp.doWhileOperacion(e1,e2,e3,e2left,e2right);:}
						;

doWhileA ::= 			instruction:e {:RESULT = e;:}
						| block:e {:RESULT = e;:}
						;

for ::= 				FOR {:mp.setEtiquetaResult(mp.getEtiqueta());:} OPEN_BRK assign:e1 COLON opBol:e2 COLON signo:e3 NUM_INTEGER:e4 CLOSE_BRK forA:e5 {:RESULT = mp.forOperacion(e1,e2,e3,e4,e5, e1left, e1right, e2left, e2right);:}
						;

forA ::= 				instruction:e {:RESULT = e;:}
						| block:e {:RESULT = e;:}
						;

scans ::= 				SCANS OPEN_SQR VARIABLE:e CLOSE_SQR SEMICOLON {:RESULT = mp.getScanString(e, eleft, eright);:}
						| SCANS OPEN_SQR arrayPosition:e CLOSE_SQR SEMICOLON {:RESULT = mp.getScanString(e, eleft, eright);:}
						;

scann ::= 				SCANN OPEN_SQR VARIABLE:e CLOSE_SQR SEMICOLON {:RESULT = mp.getScanNumber(e, eleft, eright);:}
						| SCANN OPEN_SQR arrayPosition:e CLOSE_SQR SEMICOLON {:RESULT = mp.getScanNumber(e, eleft, eright);:}
						;

print ::= 				PRINT printA:e SEMICOLON {:RESULT = e;:}
						;

println ::= 			PRINTLN printB:e SEMICOLON {:RESULT = e;:}
						;

printA ::= 				OPEN_SQR valueA:e CLOSE_SQR {:RESULT = e;:}
						;

printB ::= 				OPEN_SQR valueB:e CLOSE_SQR {:RESULT = e;:}
						;

valueA ::= 				valueA:e1 COMMA value:e2 {:RESULT = mp.getPrint(e1,e2,ManejadorParser.PRINT);:}
						| value:e {:RESULT = mp.getSimplePrint(e,ManejadorParser.PRINT);:}
						;

valueB ::= 				valueB:e1 COMMA value:e2 {:RESULT = mp.getPrint(e1,e2,ManejadorParser.PRINTLN);:}
						| value:e {:RESULT = mp.getSimplePrint(e,ManejadorParser.PRINTLN);:}
						;

value ::= 				opNum:e {:RESULT = e;:}
						;

signo ::= 				PLUS:e {:RESULT = e;:}
						| MINUS:e {:RESULT = e;:}
						;

formalAssign ::= 		assign:e SEMICOLON {:RESULT = e;:}
						;

assign ::= 				assign1:e1 exp:e2 {:RESULT = mp.asignByToken(e1,e2, e1left, e1right);:}

						;

assign1 ::= 			VARIABLE:e ASSIGN {:RESULT = mp.getVariable(e, eleft, eright);:}
						;

exp ::= 				opNum:e {:RESULT = e;:}
						;

opNum2 ::= 				ex2:e {:RESULT = mp.orderOpNum(e);:}
						;

ex2 ::= 				ex2:e1 MULTI:e2 ex2:e3 {:RESULT = mp.getMulti(e1, e2, e3);:}
						| ex2:e1 MINUS:e2 ex2:e3 {:RESULT = mp.getResta(e1, e2, e3);:}
						| ex2:e1 DIVISION:e2 ex2:e3 {:RESULT = mp.getDivision(e1, e2, e3);:}
						| ex2:e1 MODULE:e2 ex2:e3 {:RESULT = mp.getModulo(e1,e2, e3);:}
						| ex2:e1 PLUS:e2 ex2:e3 {:RESULT = mp.getSumaConcatenacion(e1, e2, e3);:}
						| OPEN_BRK ex2:e CLOSE_BRK {:RESULT = e;:}
						| num:e {:RESULT = mp.getCuartetoNum(e);:}
						| arrayPosition:e {:RESULT = mp.getArrayPosition(e);:}
						| VARIABLE:e {:RESULT = mp.getVariable(e, eleft, eright);:}
						| TEXT:e {:RESULT = mp.getCuartetoString(e, eleft, eright);:}
						| bool:e {:RESULT = mp.getCuartetoBool(e);:}
						| subprogramCall:e {:RESULT = e;:}
						;

opNum ::= 				ex:e {:RESULT = mp.orderOpNum(e);:}
						;

ex ::= 					ex:e1 MULTI:e2 ex:e3 {:RESULT = mp.getMulti(e1, e2, e3);:}
						| ex:e1 MINUS:e2 ex:e3 {:RESULT = mp.getResta(e1, e2, e3);:}
						| ex:e1 DIVISION:e2 ex:e3 {:RESULT = mp.getDivision(e1, e2, e3);:}
						| ex:e1 MODULE:e2 ex:e3 {:RESULT = mp.getModulo(e1,e2, e3);:}
						| ex:e1 PLUS:e2 ex:e3 {:RESULT = mp.getSumaConcatenacion(e1, e2, e3);:}
						| OPEN_BRK ex:e CLOSE_BRK {:RESULT = e;:}
						| num:e {:RESULT = mp.getCuartetoNum(e);:}
						| arrayPosition:e {:RESULT = mp.getArrayPosition(e);:}
						| VARIABLE:e {:RESULT = mp.getVariable(e, eleft, eright);:}
						| TEXT:e {:RESULT = mp.getCuartetoString(e, eleft, eright);:}
						| bool:e {:RESULT = mp.getCuartetoBool(e);:}
						| subprogramCall:e {:RESULT = e;:}
						;

num ::= 				NUM_INTEGER:e {:RESULT = mp.getSimbolEntero(e, eleft, eright);:}
						| NUM_FLOAT:e {:RESULT = mp.getSimbolFloat(e, eleft, eright);:}
						| NUM_DOUBLE:e {:RESULT = mp.getSimbolDouble(e, eleft, eright);:}
						;

opBol ::= 				b:e {:RESULT = e;:}
						;

b ::= 					b:e1 AND:e2 b:e3 {:RESULT = mp.getAnd(e1, e2, e3);:}
						| b:e1 OR:e2 b:e3 {:RESULT = mp.getOr(e1, e2, e3);:}
						| NOT:e1 b:e2 {:RESULT = mp.getNot(e1,e2);:}
						| OPEN_BRK b:e CLOSE_BRK {:RESULT = e;:}
						| opRel:e {:RESULT = e;:}
						| arrayPosition:e {:RESULT = mp.getArrayPosition(e);:}
						| VARIABLE:e {:RESULT = mp.getVariableRel(e, eleft, eright);:}
						| bool:e {:RESULT = mp.getBool(e);:}
						| subprogramCall:e {:RESULT = e;:}
						;

opRel ::= 				r:e {:RESULT = e;:}
						;

r ::= 					expRel:e1 rel:e2 expRel:e3 {:RESULT = mp.getRelacion(e1, e2, e3);:}
						;

rel ::= 				HIGHER:e {:RESULT = e;:}
						| LOWER:e {:RESULT = e;:}
						| HIGHER_EQ:e {:RESULT = e;:}
						| LOWER_EQ:e {:RESULT = e;:}
						| NOT_EQUAL:e {:RESULT = e;:}
						| EQUAL:e {:RESULT = e;:}
						;

expRel ::= 				er:e {:RESULT = mp.orderOpNum(e);:}
						;

er ::= 					er:e1 MULTI:e2 er:e3 {:RESULT = mp.getMulti(e1, e2, e3);:}
						| er:e1 MINUS:e2 er:e3 {:RESULT = mp.getResta(e1, e2, e3);:}
						| er:e1 DIVISION:e2 er:e3 {:RESULT = mp.getDivision(e1, e2, e3);:}
						| er:e1 MODULE:e2 er:e3 {:RESULT = mp.getModulo(e1,e2, e3);:}
						| er:e1 PLUS:e2 er:e3 {:RESULT = mp.getSumaConcatenacion(e1, e2, e3);:}
						| num:e {:RESULT = mp.getCuartetoNum(e);:}
						| arrayPosition:e {:RESULT = mp.getArrayPosition(e);:}
						| VARIABLE:e {:RESULT = mp.getVariable(e, eleft, eright);:}
						| subprogramCall:e {:RESULT = e;:}
						;

bool ::= 				TRUE:e {:RESULT = mp.getSimbolBoolean(e, eleft, eright);:}
						| FALSE:e {:RESULT = mp.getSimbolBoolean(e, eleft, eright);:}
						;

tipo ::= 				BOOLEAN:e {:RESULT = mp.getTipoByKey(e);:}
						| CHAR:e {:RESULT = mp.getTipoByKey(e);:}
						| BYTE:e {:RESULT = mp.getTipoByKey(e);:}
						| INT:e {:RESULT = mp.getTipoByKey(e);:}
						| LONG:e {:RESULT = mp.getTipoByKey(e);:}
						| FLOAT:e {:RESULT = mp.getTipoByKey(e);:}
						| DOUBLE:e {:RESULT = mp.getTipoByKey(e);:}
						| STRING:e {:RESULT = mp.getTipoByKey(e);:}
						| VOID:e {:RESULT = mp.getTipoByKey(e);:}
						;

/*
java -jar ../lib/java-cup-11b.jar AnalizadorSintactico1.cup
cp parser.java ../src/codigo3d/backend/analizadores/parser.java
*/

/*

ARRAY int [10-3][20%2][30-2] array;
void x ()[
int x;
%%%
x <- array [2-4][5*4][4-2];
]
%%%
MAIN


ARRAY int [10-3][20%2][30-2] array;
void x ()[
int x;
%%%
array[10][2][2]<- array [2][5][4];
]
%%%
MAIN


ARRAY int [10-3][20%2][30-2] array;
int x (int a)[
int x;
%%%
array[10][2][2]<- 10;
return 10;
]
int y ()[
int x;
%%%
x <- x(10);
return 10;
]
%%%
MAIN


boolean x (boolean a, boolean b)[
return FALSE;
]
boolean y ()[
return x (TRUE, FALSE);
]
%%%
MAIN


ARRAY boolean [10][20][30] booleano;
void x ()[
booleano [10][20][20] <- 10 > 2;
]
%%%
MAIN

void x ()[
int x, y, z;
ARRAY int [20+7][20][30] array1;
%%%
WHILE (array1[x +1][y+1][z+1] > y)[
PRINT[x,y,z, 2 + 3];
]
]
%%%MAIN


void x ()[
WHILE(2 > 3)[
PRINT["2 > 3"];
]
]
%%%MAIN

void x ()[
ARRAY string [2][4][4] array;
string algo;
%%%
SCANS[array[2][2][4]];
SCANS[algo];
]
%%%MAIN

void x ()[
int x, y, z;
ARRAY int [20+7][20][30] array1;
%%%
WHILE (array1[x +1][y+1][z+1] > y AND TRUE)[
PRINT[x,y,z, 2 + 3];
FOR(x <- 0: x < 20 AND FALSE: +1)[
PRINT[x];
]
]
]
%%%MAIN


void x ()[
IF (20 > 3)[
] ELSIF (20 < 3)[
] ELSIF (4 != 5)
PRINT["ALGO"]; 
 ELSE [
]
]
%%%
MAIN

void x ()[
IF (10 < 3)[
PRINT["ALGO"];
]
]
%%%MAIN

void x ()[
IF (20 > 3)[
] ELSIF (20 < 3)[
] ELSIF (4 != 5)
PRINT["ALGO"]; 
ELSIF (4 == 5)
PRINT["ALGO2"];
]
%%%
MAIN 

void x ()[
IF ((2 > 3 AND 4 < 3) OR (2 == 4 OR 3 > 4))[
PRINT["ALGO"];
] ELSIF(200< 30)[
PRINT["ALGOMAS"];
]
]
%%%MAIN

int suma (int a, int b)[
return a + b;
]
void x ()[
int x, y, z, i;
ARRAY int [20+7][20][30] array1;
%%%
i <- 0;
WHILE (array1[x +1][y+1][z+1] > y)[
PRINT[x,y,z, 2 + 3];
x <- suma(array1[x][y][z], i);
i <- i + 1;
]

]
%%%MAIN

*/